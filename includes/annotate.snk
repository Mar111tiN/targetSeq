#################################
# add information to table/{sample}_{tumor_norm}.csv
def get_anno_params(_):
    '''
    helper function to create full annovar parameters from input_output[1]
    '''

    # get the full path to humandb
    humandb = os.path.join(config['paths']['mystatic'], config['annovar']['path_to_humandb'])
    # get the available anno files
    file_list = list(os.walk(humandb))[0][-1]
    # reduce anno files to the files compatible with genome build version
    build = config['ref']['build']
    build_files = []
    for file in file_list:
        if build in file:
            build_files.append(file)

    # filter the anno protocol for the available files for that genome build        
    anno_refs = config['annovar']['annofiles']
    anno_list = []
    missing_list = []
    for anno in anno_refs:
        for file in build_files:
            if anno in file:
                anno_list.append(anno)
                break
        # if anno has not been found in file during for-loop
        else:
            missing_list.append(anno)

    # create the protocol string
    protocol = ','.join(anno_list)
    print(f"{' '.join(missing_list)} not found for {build}! Doing without.. ")
    # create the operation string 'g,r,f,f,f,f' assuming all but the first three dbs (ref, cytoBand, superDups) in config to be filter-based
    operation_list = []
    for anno in anno_list:
        if anno == "refGene":
            operation_list.append('g')
        elif anno in ['cytoBand', 'genomicSuperDups']:
            operation_list.append('r')
        else:
            operation_list.append('f')
    operation = ','.join(operation_list)

    options = f'{humandb}/ -buildver {build} -remove -thread {config["annovar"]["threads"]} -protocol {protocol} -operation {operation} -nastring "." -otherinfo'
    return options


rule annovar:
    input:
        "table/{sample}_{tumor_norm}.csv"
    output:
        "table/{sample}_{tumor_norm}.anno.csv"
    log:
        "logs/annovar/{sample}_{tumor_norm}.log"
    threads:
        config["annovar"]["threads"]
    conda:
        "../env/anno-env.yml"
    params:
        anno = get_anno_params,
        anno_info = get_script('anno_info')
    script:
        "../scripts/anno.py"


rule fisher_strand:
    input:
        "table/{sample}_{tumor_norm}.csv"
    output:
        "table/{sample}_{tumor_norm}.fisher.csv"
    log:
        "logs/fisher/{sample}_{tumor_norm}.log"
    threads:
        config["Fisher_strand"]["threads"]
    conda:
        "../env/fisher-env.yml"
    script:
        "../scripts/fisher_strand.py"


rule combine_annotations:
    input:
        annovar = "table/{sample}_{tumor_norm}.anno.csv",
        fisher = "table/{sample}_{tumor_norm}.fisher.csv",
        eb_score = "table/{sample}_{tumor_norm}.EB.csv"
    output:
        "table/{sample}_{tumor_norm}.raw.csv"
    threads:
        2
    script:
        "../scripts/format_table.py"


rule edit_mutation_list:
    input:
        "table/{sample}_{tumor_norm}.raw.csv"
    output:
        "filter/{sample}_{tumor_norm}.csv"
    threads:
        2
    params:
        extended_output = config['filter']['edit']['extended_output'],
        candidate_list = config['filter']['edit']['candidate_list'],
        driver_list = config['filter']['edit']['driver_list']
    script:
        "../scripts/edit_table.py"
